plugins {
  id 'groovy'
  id 'java'
  id 'osgi'
  id 'com.jfrog.bintray' version '1.7.1'
  id 'maven-publish'
}

repositories {
  jcenter()
}

defaultTasks 'build'

dependencies {
  compile gradleApi()
  compile localGroovy()

  compile 'org.eclipse.jgit:org.eclipse.jgit:4.4.1.201607150455-r'

  compile "com.jcraft:jsch:0.1.53"
  compile 'com.jcraft:jsch.agentproxy.connector-factory:0.0.9'
  compile 'com.jcraft:jsch.agentproxy.jsch:0.0.9'

  testCompile 'junit:junit:4.12'
}

group = 'com.palominolabs.gradle.task'
version = '0.0.4-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = JavaVersion.VERSION_1_7
compileJava.options.encoding = 'UTF-8'

task sourceJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
}

publishing {
  publications {
    bintray(MavenPublication) {
      from components.java
      groupId project.group
      artifactId project.name
      version project.version

      artifact sourceJar {
        classifier 'sources'
      }

      artifact javadocJar {
        classifier 'javadoc'
      }
    }
  }
}

bintray {
  user = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : 'FIXME'
  key = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : 'FIXME'
  publications = ['bintray']

  pkg {
    repo = 'maven'
    licenses = ['Copyfree']
    vcsUrl = 'https://github.com/palominolabs/gradle-git-clone-task'
    name = 'com.palominolabs.gradle.task:gradle-git-clone-task'

    version {
      name = project.version
      released = new Date()
      if (project.isReleaseVersion) {
        vcsTag = 'v' + project.version
      }
    }
  }
}